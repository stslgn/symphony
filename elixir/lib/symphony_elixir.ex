defmodule SymphonyElixir do
  @moduledoc """
  Entry point for the Symphony orchestrator.
  """

  @doc """
  Start the orchestrator in the current BEAM node.
  """
  @spec start_link(keyword()) :: GenServer.on_start()
  def start_link(opts \\ []) do
    SymphonyElixir.Orchestrator.start_link(opts)
  end
end

defmodule SymphonyElixir.Application do
  @moduledoc """
  OTP application entrypoint that starts core supervisors and workers.
  """

  use Application

  @impl true
  def start(_type, _args) do
    :ok = SymphonyElixir.LogFile.configure()

    Supervisor.start_link(
      child_specs(),
      strategy: supervisor_strategy(),
      name: SymphonyElixir.Supervisor
    )
  end

  @spec child_specs() :: [Supervisor.child_spec() | {module(), term()} | module()]
  def child_specs do
    [
      {Phoenix.PubSub, name: SymphonyElixir.PubSub},
      {Registry, keys: :unique, name: SymphonyElixir.PollTaskRegistry},
      SymphonyElixir.WorkflowStore,
      SymphonyElixir.StartupAttestation,
      {Task.Supervisor, name: SymphonyElixir.TaskSupervisor},
      {DynamicSupervisor, strategy: :one_for_one, name: SymphonyElixir.PollGuardSupervisor},
      {SymphonyElixir.Orchestrator, startup_settings_fn: &SymphonyElixir.StartupAttestation.settings_with_authority!/0},
      SymphonyElixir.HttpServer,
      SymphonyElixir.StatusDashboard
    ]
  end

  @spec supervisor_strategy() :: :rest_for_one
  def supervisor_strategy, do: :rest_for_one

  @impl true
  def stop(_state) do
    SymphonyElixir.StatusDashboard.render_offline_status()
    :ok
  end
end
